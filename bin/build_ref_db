#!/usr/bin/env perl

=pod

=head1 NAME

    00_build_ref_db

=head1 SYNOPSIS

    00_build_ref_db --db [db_name] 

=head1 DESCRIPTION

    Creates a reference database for use by align_reads. 
    
=head1 REQUIRED ARGUMENTS

=over 4

=item B<db>: name of database to build

=back

=head1 OPTIONAL ARGUMENTS

=over

=item B<help>: Display help text

=item B<man>: Display manual page

=back

=head1 AUTHOR - James Abbott

Email j.abbott@imperial.ac.uk

=cut

use warnings;
use strict;

use FindBin;
use Getopt::Long;
use Pod::Usage;
use Carp qw(croak cluck);
use File::Path qw(make_path rmtree);
use Bio::SeqIO;
use Bio::DB::EUtilities;
use LWP;
use XML::Simple;
use HTML::Entities;
use Net::FTP::Recursive;
use Parallel::ForkManager;
use DateTime;

use lib "$FindBin::Bin/../lib";
use SCaMP;

my $scamp_root;
if ( $ENV{'SCAMP_ROOT'} ) {
    $scamp_root = $ENV{'SCAMP_ROOT'};
}
else {
    $scamp_root = "$FindBin::Bin/..";
}

{

    my ( $format, $db, $help, $man, $db_data );
    GetOptions(
                'db:s'     => \$db,
                'format:s' => \$format,
                'help'     => \$help,
                'man'      => \$man,
              );

    croak "\nUnknown argument: @ARGV" if "@ARGV";
    pod2usage( verbose => 2 ) if ($man);
    pod2usage( verbose => 1 ) if ( $help || !$db );

    my $scamp = SCaMP->new( 'scamp_root' => $scamp_root );

    my $dt      = DateTime->now();
    my $date    = $dt->ymd();
    my $out_dir = $scamp->get_database_dir . "/$db/$date";
    my $in_dir  = $scamp->get_database_dir() . "/raw_data";

    unless ( -d $out_dir ) {
        make_path( $out_dir, { error => \my $err } );
        if (@$err) {
            for my $diag (@$err) {
                my ( $file, $message ) = %$diag;
                if ( $file eq '' ) {
                    print "general error: $message\n";
                }
                else {
                    print "problem creating $file: $message\n";
                }
            }
        }
    }

    rmtree("$out_dir/split") if ( -e "$out_dir/split" );
    mkdir "$out_dir/split" or croak "Error creating $out_dir/split: $!";

    my $dbs = $scamp->get_databases();
    foreach my $db_name ( keys %{$dbs} ) {
        $db_data = $dbs->{$db_name} if ( $db_name eq $db );
    }

    unless ( defined($db_data) ) {
        croak "$db database is not defined";
    }

    if ( !$format ) {
        foreach my $val (@$db_data) {
            if ( $val->{'format'} ) {
                $format = $val->{'format'};
            }
        }
    }

    $in_dir = download( $db, $db_data, $in_dir, $date );
    uncompress($in_dir);
    reformat( $db, $format, $in_dir, $out_dir );

    print "\nBuilding BWA index...\n";
    my $cmd = "bwa index $out_dir/$db";
    system($cmd) == 0 or croak "Error executing $cmd:$!";

    my $latest = $scamp->get_database_dir() . "/$db/latest";
    if ( -l $latest ) {
        unlink $latest or die "Error removing $latest symlink: $!";
    }
    chdir "$out_dir/.." or die "Error chdiring to $out_dir/..:$ !";
    symlink( $date, $latest ) or die "Error creating $latest symlink: $!";

}

######################################################################
#
# download
#
# downloads a database defined in YAML configuration into
# $dir...
#
# required params: $ (db_name)
#                  $ (hashref of dbdata)
#                  $ (base dir to write database)
#
# returns:         $ (path database written to)
#
######################################################################

sub download {

    my $db      = shift;
    my $db_data = shift;
    my $dir     = shift;
    my $date    = shift;

    my $path;
    unless ( -d $dir ) {
        make_path( $dir, { error => \my $err } );
        if (@$err) {
            for my $diag (@$err) {
                my ( $file, $message ) = %$diag;
                if ( $file eq '' ) {
                    print "general error: $message\n";
                }
                else {
                    print "problem unlinking $file: $message\n";
                }
            }
        }
    }

    foreach my $dat (@$db_data) {
        if ( $dat->{'type'} && $dat->{'type'} eq 'ftp' ) {
            print "\nDownloading $db via ftp...\n";
            $path = ftp_download( $db, $db_data, $dir, $date );
        }
        elsif ( $dat->{'type'} && $dat->{'type'} eq 'entrez' ) {
            print "\nDownloading $db via entrez...\n";
            $path = entrez_download( $db, $db_data, $dir );
        }
        elsif ( $dat->{'type'} && $dat->{'type'} eq 'entrez_accessions' ) {
            print "\nDownloading $db via entrez accessions...\n";
            $path = entrez_accession_download( $db, $db_data, $dir );
        }
        elsif ( $dat->{'type'} && $dat->{'type'} eq 'entrez_assembly' ) {
            print "\nDownloading $db via entrez assemblies...\n";
            $path = entrez_assembly_download( $db, $db_data, $dir );
        }
    }
    return ($path);
}

######################################################################
#
# ftp_download
#
# Downloads database via ftp...
#
# required params: $ (db name)
#                  $ (hasref of dbdata)
#                  $ (base dir to write database to)
#                  $ (date)
#
# returns:         $ (path database written to)
#
######################################################################

sub ftp_download {

    my $db      = shift;
    my $db_data = shift;
    my $dir     = shift;
    my $date    = shift;

    my ( $ftp_host, $ftp_dir, $file_pattern, $format );
    foreach my $val (@$db_data) {
        if ( $val->{'ftphost'} ) {
            $ftp_host = $val->{'ftphost'};
        }
        elsif ( $val->{'ftpdir'} ) {
            $ftp_dir = $val->{'ftpdir'};
        }
        elsif ( $val->{'file_pattern'} ) {
            $file_pattern = $val->{'file_pattern'};
        }
        elsif ( $val->{'format'} ) {
            $format = $val->{'format'};
        }
    }

    rmtree($dir) if ( -d $dir );
    mkdir($dir) or croak "Error creating $dir: $!";

    my $ftp = Net::FTP::Recursive->new( $ftp_host, Passive => 1, Debug => 0, Timeout => 480 )
      or croak "Cannot connect to $ftp_host:$!";
    $ftp->login( "anonymous", 'bsshelp@imperial.ac.uk' ) or croak "Cannot login: " . $ftp->message;
    $ftp->binary();
    $ftp->cwd($ftp_dir) or croak "Cannot chdir to $ftp_dir: " . $ftp->message;

    my $rem_dir = $ftp->pwd();

    # this will only work with ensembl format databases...
    my $release;

    if ( $rem_dir =~ /release-([0-9]+)/ ) {
        $release = $1;
    }
    else {
        $release = $date;
    }

    mkdir("$dir/$release") or croak "Could not mkdir $dir/$release: $!" if ( !-d "$dir/$release" );
    chdir("$dir/$release") or croak "Could not chdir to $dir/$release:$!";

    $ftp->rget( MatchFiles => $file_pattern, FlattenTree => 1 );

    return ("$dir/$release");

}

######################################################################
#
# entrez_download
#
# Downloads specified database via entrez
#
# required params: $ (db name)
#                  $ (hasref of dbdata)
#                  $ (base dir to write database to)
#
# returns:         $ (path database written to)
#
######################################################################

sub entrez_download {

    my $db      = shift;
    my $db_data = shift;
    my $dir     = shift;

    my ( $query, $format );
    foreach my $val (@$db_data) {
        if ( $val->{'entrez_query'} ) {
            $query = $val->{'entrez_query'};
        }
        elsif ( $val->{'format'} ) {
            $format = $val->{'format'};
        }
    }

    my $email = `getent aliases $ENV{'USER'}|awk '{print \$2}'` || 'medbio-help@imperial.ac.uk';

    rmtree($dir) if ( -d $dir );
    mkdir($dir) or croak "Error creating $dir: $!";

    my $factory = Bio::DB::EUtilities->new(
                                            -eutil      => 'esearch',
                                            -email      => $email,
                                            -db         => 'nuccore',
                                            -term       => $query,
                                            -usehistory => 'y'
                                          );

    my $count = $factory->get_count;
    my $hist = $factory->next_History || croak 'No history data returned';
    $factory->set_parameters(
                              -eutil   => 'efetch',
                              -rettype => 'gb',
                              -history => $hist
                            );

    my $retry = 0;
    my ( $retmax, $retstart ) = ( 500, 0 );

    open( my $out, '>', "$dir/$db.gb" ) || croak "Can't open file:$!";

  RETRIEVE_SEQS:
    while ( $retstart < $count ) {
        $factory->set_parameters( -retmax   => $retmax,
                                  -retstart => $retstart );
        eval {
            $factory->get_Response( -cb => sub { my ($data) = @_; print $out $data } );
        };
        if ($@) {
            croak "Server error: $@.  Try again later" if $retry == 5;
            print STDERR "Server error, redo #$retry\n";
            $retry++ && redo RETRIEVE_SEQS;
        }
        $retstart += $retmax;
    }

    close $out;

    return ($dir);
}

######################################################################
#
# entrez_accession_download
#
# Downloads specified database via entrez
#
# required params: $ (db name)
#                  $ (hasref of dbdata)
#                  $ (base dir to write database to)
#
# returns:         $ (path database written to)
#
######################################################################

sub entrez_accession_download {

    my $db      = shift;
    my $db_data = shift;
    my $dir     = shift;

    my ( $query, $format, $accessions );
    foreach my $val (@$db_data) {
        if ( $val->{'entrez_query'} ) {
            $query = $val->{'entrez_query'};
        }
        elsif ( $val->{'format'} ) {
            $format = $val->{'format'};
        }
        elsif ( $val->{'accessions'} ) {
            $accessions = $val->{'accessions'};
        }
    }

    my $email = `getent aliases $ENV{'USER'}|awk '{print \$2}'` || 'medbio-help@imperial.ac.uk';

    rmtree($dir) if ( -d $dir );
    mkdir($dir) or croak "Error creating $dir: $!";

    my $factory = Bio::DB::EUtilities->new(
                                            -eutil   => 'efetch',
                                            -db      => 'nucleotide',
                                            -rettype => 'gb',
                                            -email   => $email,
                                            -id      => $accessions
                                          );

    my $file = "$dir/$db.gb";
    $factory->get_Response( -file => $file );

    my $seqin = Bio::SeqIO->new( -file   => $file,
                                 -format => 'genbank' );

    while ( my $seq = $seqin->next_seq ) {

        # do whatever....
        print "Retrieving ", $seq->display_id(), "...\n";
        my $anno_collection = $seq->annotation;
        my @wgs             = $anno_collection->get_Annotations('wgs');
        my @wgs_accessions;
        foreach my $wgs (@wgs) {
            if ( $wgs->tagname eq 'wgs' ) {
                my ( $start, $end ) = split( /-/, $wgs->value );
                my $prefix = $1 if ( $start =~ /^([A-Z]{4})/ );
                $start =~ s/$prefix//;
                $end =~ s/$prefix//;
                $start =~ s/^0+//;
                $end =~ s/^0+//;
                for ( my $i = $start ; $i <= $end ; $i++ ) {
                    my ($acc);
                    if ( length($start) == 8 ) {
                        $acc = sprintf( "%s%09s", $prefix, $i );
                    }
                    elsif ( length($start) == 7 ) {
                        $acc = sprintf( "%s%08s", $prefix, $i );
                    }
                    else {
                        croak "Unhandled accession length: $wgs->value()";
                    }
                    push @wgs_accessions, $acc;
                }
            }
        }

        # retreive sequences in batches of 500....
        my $retry = 0;
        my ( $retmax, $retstart ) = ( 500, 0 );
        my $file = "$dir/$db." . $seq->display_id . ".gb";

        open( my $out, '>', "$file" ) || croak "Can't open file:$!";

      RETRIEVE_SEQS:
        while ( $retstart < $#wgs_accessions ) {
            my @query_accs = @wgs_accessions[ $retstart .. ( $retstart + ( $retmax - 1 ) ) ];
            print "\tGetting accessions $retstart -> " . ( $retstart + ( $retmax - 1 ) ) . "....\n";
            my $wgs_factory = Bio::DB::EUtilities->new(
                                                        -eutil   => 'efetch',
                                                        -db      => 'nucleotide',
                                                        -rettype => 'gb',
                                                        -email   => $email,
                                                        -id      => \@query_accs,
                                                      );
            eval {
                $wgs_factory->get_Response( -cb => sub { my ($data) = @_; print $out $data } );
            };
            if ($@) {
                croak "Server error: $@.  Try again later" if $retry == 5;
                print STDERR "Server error, redo #$retry\n";
                $retry++ && redo RETRIEVE_SEQS;
            }
            $retstart += $retmax;
        }

        close $out;
    }
    return ($dir);
}

######################################################################
#
# entrez_assembly_download
#
# Downloads database consisting of refseq/genbank assembly IDs
# Looks up WGS accession for each assembly, then passes these to
# entrez_accession_download
#
# required params: $ (db name)
#                  $ (hasref of dbdata)
#                  $ (base dir to write database to)
#
# returns:         $ (path database written to)
#
######################################################################

sub entrez_assembly_download {

    my $db      = shift;
    my $db_data = shift;
    my $dir     = shift;

    my ( $query, $format, $accessions );
    foreach my $val (@$db_data) {
        if ( $val->{'entrez_query'} ) {
            $query = $val->{'entrez_query'};
        }
        elsif ( $val->{'format'} ) {
            $format = $val->{'format'};
        }
        elsif ( $val->{'accessions'} ) {
            $accessions = $val->{'accessions'};
        }
    }

    my @wgs_accessions;
    my $ua = LWP::UserAgent->new;

    foreach my $accession (@$accessions) {
        print "Checking $accession...\n";
        my $assembly_url = "https://www.ncbi.nlm.nih.gov/assembly/$accession/?report=xml&format=text";
        my $req          = HTTP::Request->new( GET => $assembly_url );
        my $res          = $ua->request($req);

        if ( $res->is_success() ) {

            my ( $xml, $xml_ref );
            eval {
                $xml     = decode_entities( $res->content() );
                $xml_ref = XMLin($xml);
            };
            my $wgs_short;

            if ( ref( $xml_ref->{'DocumentSummary'} ) eq 'ARRAY' ) {
                $wgs_short = $xml_ref->{'DocumentSummary'}->[0]->{'WGS'};
            }
            else {
                $wgs_short = $xml_ref->{'DocumentSummary'}->{'WGS'};
            }

            if ( $wgs_short && $wgs_short =~ /([A-Z]{4})([0-9]{2})/ ) {
                my $prefix = $1;
                my $ver    = sprintf( "%u", $2 );
                my $acc    = $prefix . '0' x 8 . "." . $ver;
                push @wgs_accessions, $acc;
            }
            else {
                warn "WGS accession could not be found for assembly $accession";
            }
        }
        else {
            croak "Could not retrieve $assembly_url: $!";
        }
    }
    my @new_db_data;
    foreach my $val (@$db_data) {
        if ( $val->{'accessions'} ) {
            $val->{'accessions'} = \@wgs_accessions;
        }
        push @new_db_data, $val;
    }
    print "...done\n\n";
    $dir = entrez_accession_download( $db, \@new_db_data, $dir );

    return ($dir);
}
######################################################################
#
# uncompress
#
# uncompresses downloaded files...as long as they are gzipped!
#
# required params: $ (path to uncompress files in)
#
# returns: none
#
######################################################################

sub uncompress {

    my $dir = shift;

    print "\nUncompressing...\n";

    chdir $dir or croak "Could not chdir to $dir:$!";
    opendir DIR, $dir or croak "Could not open $dir:$!";
    my @files = grep /gz$/, readdir DIR;
    close DIR;

    my $pm = new Parallel::ForkManager(4);
    foreach my $file (@files) {
        $pm->start and next();
        my $cmd = "/usr/bin/gunzip $dir/$file";
        system($cmd) == 0 or croak "Error executing $cmd:$!";
        $pm->finish;
    }
    $pm->wait_all_children();
}

######################################################################
#
# reformat
#
# Converts to fasta and builds taxonomy lookup table
#
# required params:
#
# returns: $
#
######################################################################

sub reformat {

    my $db      = shift;
    my $format  = shift;
    my $in_dir  = shift;
    my $out_dir = shift;

    opendir IN, $in_dir or croak "Error opening $in_dir:$ !";
    my @in_files = grep !/\.\.?\z/, readdir IN;
    close IN;

    print "\nConverting $db to fasta format...\n\n";
    my $pm = new Parallel::ForkManager(8);
    my %ids;
    foreach my $in_file (@in_files) {
        $pm->start and next();
        my $inIO = Bio::SeqIO->new( -format => $format, -file => "$in_dir/$in_file" );
        my $out_file = $in_file;
        $out_file .= ".$$.fasta";

        my $outIO = Bio::SeqIO->new( -format => 'fasta', -file => ">$out_dir/split/$out_file" );
        my $tax_out = $out_dir . "/split/" . "$db.$in_file.$$.taxid.dat";
        open TAXOUT, ">$tax_out" or croak "Error opening $tax_out: $!";
        print TAXOUT "##Accession\tSpecies\tStrain\tNCBI Tax Id\n";
        while ( my $record = $inIO->next_seq() ) {
            my ( $species, $sub_species, $tax_id );
            $sub_species = "";
            $species     = $record->species->binomial();
            $sub_species = $record->species->sub_species() if ( $record->species->sub_species() );
            $tax_id      = $record->species->ncbi_taxid();

            # Handle non-unique IDs in databases (yes ensembl, I'm looking at you...) by appending a
            # count to the end of the ID
            ( exists $ids{ $record->display_id() } )
              ? ( $ids{ $record->display_id }++ )
              : ( $ids{ $record->display_id } = 1 );

            print "ID: ", $record->display_id(), "\tcount=", $ids{ $record->display_id }, "\n";
            $record->display_id( $record->display_id . "_" . $ids{ $record->display_id } );

            #prepend accession with tax_id since ensembl etc. aren't necessarily unique...
            $record->display_id( $tax_id . "_" . $record->display_id );

            print TAXOUT $record->display_id(), "\t", $species, "\t", $sub_species, "\t", $tax_id, "\n";
            $outIO->write_seq($record);
        }
        close TAXOUT;
        $pm->finish();
    }
    $pm->wait_all_children();

    print "\nMerging fasta files...\n";
    opendir DIR, "$out_dir/split" or croak "Error opening $out_dir: $!";
    my @fastas = grep /fasta$/, readdir DIR;
    rewinddir DIR;
    my @tax = grep /taxid.dat$/, readdir DIR;
    close DIR;

    my $outIO = Bio::SeqIO->new( -format => 'fasta', -file => ">$out_dir/$db" );
    foreach my $fasta (@fastas) {
        my $inIO = Bio::SeqIO->new( -format => 'fasta', -file => "$out_dir/split/$fasta" );
        while ( my $seq = $inIO->next_seq() ) {
            $outIO->write_seq($seq);
        }
        unlink "$out_dir/split/$fasta" or croak "Error removing $out_dir/split/$fasta: $!";
    }

    print "\nMerging tax.dat files...\n";
    open TAX, ">$out_dir/$db.taxid.dat\n";
    foreach my $tax (@tax) {
        open INTAX, "$out_dir/split/$tax" or croak "Error opening $out_dir/split/$tax: $!";
        while (<INTAX>) {
            print TAX $_;
        }
        close INTAX;
        unlink "$out_dir/split/$tax" or croak "Error removing $out_dir/split/$tax: $!";
    }
    close TAX;

    rmdir "$out_dir/split" or croak "Error removing $out_dir/split: $!";

}

